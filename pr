CREATE PROCEDURE [stg].[usp_Customer_Ingestion_UpdateCustomerIsValidMobile]
AS
BEGIN
    SET NOCOUNT ON;
    SET XACT_ABORT ON;  -- Automatically rolls back on runtime errors!

    BEGIN TRY
        BEGIN TRAN;

        -- Update existing rows in stg.Customer
        UPDATE c
        SET c.IsValidMobile = cbs.IsValidMobile,
            c.ValidPhoneNumber = cbs.ValidPhoneNumber
        FROM stg.Customer c
        INNER JOIN stg.CustomerMobileStatus cbs 
            ON c.CustomerId = cbs.CustomerId;

        -- AMR - If a customer's phone number has changed and there is no corresponding row in the CDC/staging table,
        -- the record will not exist in stg.Customer. In such cases, we need to ensure that the master record
        -- in dbo.Customer is updated directly.

        UPDATE dc
        SET dc.IsValidMobile = cbs.IsValidMobile,
            dc.ValidPhoneNumber = cbs.ValidPhoneNumber
        FROM dbo.Customer dc
        INNER JOIN stg.CustomerMobileStatus cbs 
            ON dc.CustomerId = cbs.CustomerId
        WHERE NOT EXISTS (
            SELECT 1
            FROM stg.Customer c
            WHERE c.CustomerId = dc.CustomerId
        );

        COMMIT TRAN;
    END TRY
    BEGIN CATCH
        -- Rollback if any error occurs and throw an error back to ADF pipeline
        IF XACT_STATE() <> 0
            ROLLBACK TRAN;
        THROW;
    END CATCH
END;
