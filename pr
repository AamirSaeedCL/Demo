DECLARE @LastRunDateTime DATETIME2 = TRY_CAST('2025-01-01' AS DATETIME2);

DECLARE @LastRunDateTimeLocal DATETIME2 =
    CAST(
        SWITCHOFFSET(
            @LastRunDateTime AT TIME ZONE 'UTC',
            DATENAME(TzOffset, SYSDATETIMEOFFSET() AT TIME ZONE 'GMT Standard Time')
        ) AS DATETIME2
    );

DECLARE @from_lsn BINARY(10);
DECLARE @to_lsn   BINARY(10);

SELECT @from_lsn = MIN(start_lsn)
FROM cdc.lsn_time_mapping
WHERE tran_end_time >= @LastRunDateTimeLocal;

SET @to_lsn = sys.fn_cdc_get_max_lsn();

IF @from_lsn IS NULL
    SET @from_lsn = sys.fn_cdc_get_min_lsn('dbo_cnsmr_tag');

;WITH LatestCDC AS (
    SELECT
        ct.cnsmr_id,
        ct.tag_id,
        ct.cnsmr_tag_assgn_dt,
        ct.cnsmr_tag_sft_delete_flg,
        ct.upsrt_dttm,
        ROW_NUMBER() OVER (
            PARTITION BY ct.cnsmr_id, ct.tag_id
            ORDER BY ct.__$start_lsn DESC
        ) AS rn
    FROM cdc.fn_cdc_get_all_changes_dbo_cnsmr_tag(@from_lsn, @to_lsn, 'all') ct
    WHERE ct.__$operation IN (2, 4)
),
FilteredCDC AS (
    SELECT *
    FROM LatestCDC
    WHERE rn = 1
),
ChangedCustomers AS (
    SELECT DISTINCT cnsmr_id
    FROM FilteredCDC
),
TagData AS (
    SELECT
        c.cnsmr_id,
        c.cnsmr_idntfr_lgcy_txt,
        t.tag_typ_id,
        UPPER(tt.tag_typ_shrt_nm)                         AS tag_typ_shrt_nm,
        t.tag_shrt_nm,
        COALESCE(cdc.cnsmr_tag_assgn_dt, ct.cnsmr_tag_assgn_dt) AS cnsmr_tag_assgn_dt,
        CASE
            WHEN COALESCE(cdc.cnsmr_tag_sft_delete_flg, ct.cnsmr_tag_sft_delete_flg) = 'Y'
            THEN COALESCE(cdc.upsrt_dttm, ct.upsrt_dttm)
        END                                               AS tag_removed_dt,
        CASE
            WHEN COALESCE(cdc.cnsmr_tag_sft_delete_flg, ct.cnsmr_tag_sft_delete_flg) = 'Y'
            THEN 0 ELSE 1
        END                                               AS tag_active,
        COALESCE(cdc.cnsmr_tag_sft_delete_flg, ct.cnsmr_tag_sft_delete_flg) AS cnsmr_tag_sft_delete_flg,
        COALESCE(cdc.upsrt_dttm, ct.upsrt_dttm)            AS upsrt_dttm
    FROM dbo.cnsmr c WITH (NOLOCK)
    JOIN dbo.cnsmr_tag ct WITH (NOLOCK)
        ON ct.cnsmr_id = c.cnsmr_id
    LEFT JOIN FilteredCDC cdc
        ON cdc.cnsmr_id = ct.cnsmr_id
       AND cdc.tag_id  = ct.tag_id
    JOIN dbo.tag t WITH (NOLOCK)
        ON t.tag_id = ct.tag_id
    JOIN dbo.tag_typ tt WITH (NOLOCK)
        ON tt.tag_typ_id = t.tag_typ_id
    WHERE UPPER(tt.tag_typ_shrt_nm) IN
        ('DDRETCD','DIAL_TAG','DIGITAL','PAYPHASE','PHASE',
         'PLANSTAT','RMDTN','PREDEF','SMS','PLATSTAT')
),
TagDataLatest AS (
    SELECT *
    FROM (
        SELECT
            td.*,
            ROW_NUMBER() OVER (
                PARTITION BY td.cnsmr_id, td.tag_typ_shrt_nm
                ORDER BY td.upsrt_dttm DESC
            ) AS rn
        FROM TagData td
    ) x
    WHERE rn = 1
)

SELECT
    c.cnsmr_id                              AS CustomerId,
    c.cnsmr_idntfr_lgcy_txt                 AS LegacyId,
    u.UpsertDateTime,

    dd.tag_shrt_nm                          AS DDTag,
    dd.cnsmr_tag_assgn_dt                   AS DateDDAssigned,
    dd.tag_removed_dt                      AS DateDDRemoved,
    CAST(dd.tag_active AS bit)              AS DDActive,

    dial.tag_shrt_nm                        AS DialTag,
    dial.cnsmr_tag_assgn_dt                 AS DateDialAssigned,
    dial.tag_removed_dt                    AS DateDialRemoved,
    CAST(dial.tag_active AS bit)            AS DialActive,

    dig.tag_shrt_nm                         AS DigitalTag,
    dig.cnsmr_tag_assgn_dt                  AS DateDigitalAssigned,
    dig.tag_removed_dt                     AS DateDigitalRemoved,
    CAST(dig.tag_active AS bit)             AS DigitalActive,

    pay.tag_shrt_nm                         AS PayerPhaseTag,
    pay.cnsmr_tag_assgn_dt                  AS DatePayerPhaseAssigned,
    pay.tag_removed_dt                     AS DatePayerPhaseRemoved,
    CAST(pay.tag_active AS bit)             AS PayerPhaseActive,

    stat.tag_shrt_nm                        AS StatusTag,
    stat.cnsmr_tag_assgn_dt                 AS DateStatusAssigned,
    stat.tag_removed_dt                    AS DateStatusRemoved,
    CAST(stat.tag_active AS bit)            AS StatusActive,

    prev.tag_shrt_nm                        AS PreviousStatusTag,
    prev.cnsmr_tag_assgn_dt                 AS DatePreviousStatusAssigned,
    prev.tag_removed_dt                    AS DatePreviousStatusRemoved,
    CAST(0 AS bit)                          AS PreviousStatusActive

FROM dbo.cnsmr c
JOIN ChangedCustomers cc
    ON cc.cnsmr_id = c.cnsmr_id

OUTER APPLY (
    SELECT MAX(upsrt_dttm) AS UpsertDateTime
    FROM TagDataLatest
    WHERE cnsmr_id = c.cnsmr_id
) u

OUTER APPLY (
    SELECT * FROM TagDataLatest
    WHERE cnsmr_id = c.cnsmr_id AND tag_typ_shrt_nm = 'DDRETCD'
) dd

OUTER APPLY (
    SELECT * FROM TagDataLatest
    WHERE cnsmr_id = c.cnsmr_id AND tag_typ_shrt_nm = 'DIAL_TAG'
) dial

OUTER APPLY (
    SELECT * FROM TagDataLatest
    WHERE cnsmr_id = c.cnsmr_id AND tag_typ_shrt_nm = 'DIGITAL'
) dig

OUTER APPLY (
    SELECT * FROM TagDataLatest
    WHERE cnsmr_id = c.cnsmr_id AND tag_typ_shrt_nm = 'PAYPHASE'
) pay

OUTER APPLY (
    SELECT * FROM TagDataLatest
    WHERE cnsmr_id = c.cnsmr_id AND tag_typ_shrt_nm = 'PLATSTAT'
) stat

OUTER APPLY (
    SELECT TOP (1)
        td.tag_shrt_nm,
        td.cnsmr_tag_assgn_dt,
        td.tag_removed_dt
    FROM TagData td
    WHERE td.cnsmr_id = c.cnsmr_id
      AND td.tag_typ_shrt_nm = 'PLATSTAT'
      AND td.cnsmr_tag_sft_delete_flg = 'Y'
    ORDER BY td.tag_removed_dt DESC
) prev

WHERE c.cnsmr_id = 1604529;
