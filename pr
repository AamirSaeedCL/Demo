DECLARE @LastRunDateTime DATETIME2 = TRY_CAST('2026-01-22T22:43:52.62' AS DATETIME2);

DECLARE @LastRunDateTimeLocal DATETIME2 = CAST(
    SWITCHOFFSET(
        @LastRunDateTime AT TIME ZONE 'UTC',
        DATENAME(TzOffset, SYSDATETIMEOFFSET() AT TIME ZONE 'GMT Standard Time')
    ) AS DATETIME2
);

DECLARE @from_lsn BINARY(10);
DECLARE @to_lsn   BINARY(10);

-- Determine LSN range for CDC
SELECT @from_lsn = MIN(start_lsn)
FROM cdc.lsn_time_mapping
WHERE tran_end_time >= @LastRunDateTimeLocal;

SET @to_lsn = sys.fn_cdc_get_max_lsn();

IF @from_lsn IS NULL
    SET @from_lsn = sys.fn_cdc_get_min_lsn('dbo_cnsmr_tag');

;WITH CdcRemovedTags AS
(
    SELECT
        ct.cnsmr_id,
        ct.tag_id,
        ct.cnsmr_tag_assgn_dt,
        ct.cnsmr_tag_sft_delete_flg,
        ct.upsrt_dttm,
        ct.__$start_lsn
    FROM cdc.fn_cdc_get_all_changes_dbo_cnsmr_tag(@from_lsn, @to_lsn, 'all') ct
    INNER JOIN dbo.tag t
        ON t.tag_id = ct.tag_id
    INNER JOIN dbo.tag_typ tt
        ON tt.tag_typ_id = t.tag_typ_id
       AND tt.tag_typ_shrt_nm = 'PLATSTAT'
    WHERE ct.__$operation IN (2, 4)  -- Inserts & Updates
      AND ct.cnsmr_tag_sft_delete_flg = 'Y'
),
LatestRemovedPerCustomer AS
(
    SELECT
        *,
        ROW_NUMBER() OVER (
            PARTITION BY cnsmr_id
            ORDER BY upsrt_dttm DESC
        ) AS rn
    FROM CdcRemovedTags
)

SELECT
    lrc.cnsmr_id          AS CustomerId,
    t.tag_shrt_nm          AS TagValue,
    lrc.cnsmr_tag_assgn_dt AS TagAssignedDate,
    lrc.upsrt_dttm         AS TagRemovedDate,
    CAST(0 AS BIT)         AS TagActive,  -- always 0 since it's removed
    lrc.upsrt_dttm         AS UpsertDateTime
FROM LatestRemovedPerCustomer lrc
JOIN dbo.tag t
    ON t.tag_id = lrc.tag_id
WHERE rn = 1
ORDER BY lrc.cnsmr_id, TagRemovedDate DESC;
