DECLARE @LastRunDateTime DATETIME2 =
    TRY_CAST(
        '2026-01-22T22:43:52.62'
        AS DATETIME2
    );

DECLARE @LastRunDateTimeLocal DATETIME2 =
    CAST(
        SWITCHOFFSET(
            @LastRunDateTime AT TIME ZONE 'UTC',
            DATENAME(
                TzOffset,
                SYSDATETIMEOFFSET() AT TIME ZONE 'GMT Standard Time'
            )
        ) AS DATETIME2
    );

WITH LatestRemovedTags AS (
    SELECT
        ct.cnsmr_id,
        t.tag_shrt_nm,
        tt.tag_typ_shrt_nm,
        ct.upsrt_dttm,
        ct.cnsmr_tag_assgn_dt,
        CASE
            WHEN ct.cnsmr_tag_sft_delete_flg = 'Y'
            THEN ct.upsrt_dttm
        END AS tag_removed_dt,
        CASE
            WHEN ct.cnsmr_tag_sft_delete_flg = 'Y'
            THEN CAST(0 AS bit)
            ELSE CAST(1 AS bit)
        END AS tag_active,
        ROW_NUMBER() OVER (
            PARTITION BY ct.cnsmr_id
            ORDER BY ct.upsrt_dttm DESC
        ) AS rn
    FROM dbo.cnsmr_tag ct
    JOIN dbo.tag t
        ON t.tag_id = ct.tag_id
    JOIN dbo.tag_typ tt
        ON tt.tag_typ_id = t.tag_typ_id
       AND tt.tag_typ_shrt_nm = 'PLATSTAT'
    WHERE ct.cnsmr_tag_sft_delete_flg = 'Y'
	AND ct.upsrt_dttm >= @LastRunDateTimeLocal
)

SELECT
    cnsmr_id           AS CustomerId,
    tag_shrt_nm        AS TagValue,
    cnsmr_tag_assgn_dt AS TagAssignedDate,
    tag_removed_dt     AS TagRemovedDate,
    tag_active         AS TagActive,
    upsrt_dttm         AS UpsertDateTime
FROM LatestRemovedTags
WHERE rn = 1
ORDER BY cnsmr_id, TagRemovedDate DESC;


==============

DECLARE @from_lsn BINARY(10);
DECLARE @to_lsn   BINARY(10);

SELECT
    @from_lsn = MIN(start_lsn)
FROM cdc.lsn_time_mapping
WHERE tran_end_time >= @LastRunDateTimeLocal;

SET @to_lsn = sys.fn_cdc_get_max_lsn();

IF @from_lsn IS NULL
    SET @from_lsn = sys.fn_cdc_get_min_lsn('dbo_cnsmr_tag');

;WITH CdcWithTagType AS
(
    SELECT
        ct.cnsmr_id,
        ct.tag_id,
        ct.cnsmr_tag_assgn_dt,
        ct.cnsmr_tag_sft_delete_flg,
        ct.upsrt_dttm,
        ct.__$start_lsn
    FROM cdc.fn_cdc_get_all_changes_dbo_cnsmr_tag
         (@from_lsn, @to_lsn, 'all') ct
    INNER JOIN dbo.tag t
        ON t.tag_id = ct.tag_id
       AND t.tag_typ_id = @TagTypeId
    WHERE ct.__$operation IN (2, 4)
),
LatestPerTag AS
(
    SELECT
        *,
        ROW_NUMBER() OVER
        (
            PARTITION BY cnsmr_id, tag_id
            ORDER BY __$start_lsn DESC
        ) AS tag_rn
    FROM CdcWithTagType
),
LatestPerCustomer AS
(
    SELECT
        *,
        ROW_NUMBER() OVER
        (
            PARTITION BY cnsmr_id
            ORDER BY upsrt_dttm DESC
        ) AS cust_rn
    FROM LatestPerTag
    WHERE tag_rn = 1
)

SELECT
    cnsmr_id              AS CustomerId,
    tag_shrt_nm           AS TagValue,
    cnsmr_tag_assgn_dt    AS TagAssignedDate,
    CASE
        WHEN cnsmr_tag_sft_delete_flg = 'Y'
            THEN lpc.upsrt_dttm
    END                   AS TagRemovedDate,
    CASE
        WHEN cnsmr_tag_sft_delete_flg = 'Y'
            THEN CAST(0 AS BIT)
        ELSE CAST(1 AS BIT)
    END                   AS TagActive,
    lpc.upsrt_dttm            AS UpsertDateTime
FROM LatestPerCustomer lpc
JOIN dbo.tag t
    ON t.tag_id = lpc.tag_id
WHERE cust_rn = 1
ORDER BY cnsmr_id;
