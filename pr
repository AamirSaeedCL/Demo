CREATE PROCEDURE [dbo].[ChangeTrackingStatusAllTables]
    @Enable BIT
AS
BEGIN
    SET NOCOUNT ON;

    DECLARE @Schema NVARCHAR(128) = 'dbo';
    DECLARE @TableName NVARCHAR(128);
    DECLARE @SQL NVARCHAR(MAX);
    DECLARE @Index INT = 1;
    DECLARE @TableCount INT;

    -- List of tables
    DECLARE @Tables TABLE (ID INT IDENTITY(1,1), TableName NVARCHAR(128));
    INSERT INTO @Tables (TableName)
    VALUES 
        ('Account'),
        ('AccountTag'),
        ('Bureau'),
        ('Customer'),
        ('CustomerAddress'),
        ('CustomerPhone'),
        ('CustomerTag'),
        ('Digital'),
        ('Eligibility'),
        ('FinancialStrength'),
        ('Interaction'),
        ('ModelledNdi'),
        ('PaymentPlan'),
        ('SalesForceInteraction'),
        ('Score'),
        ('Sfs'),
        ('WorkGroup');

    SET @TableCount = (SELECT COUNT(*) FROM @Tables);

    -- If disabling, process table-level first, then disable DB-level
    IF @Enable = 0
    BEGIN
        WHILE @Index <= @TableCount
        BEGIN
            SELECT @TableName = TableName FROM @Tables WHERE ID = @Index;

            IF EXISTS (
                SELECT 1 FROM sys.change_tracking_tables 
                WHERE object_id = OBJECT_ID(QUOTENAME(@Schema) + '.' + QUOTENAME(@TableName))
            )
            BEGIN
                SET @SQL = 'ALTER TABLE [' + @Schema + '].[' + @TableName + '] DISABLE CHANGE_TRACKING';
                EXEC (@SQL);
            END

            SET @Index += 1;
        END

        -- Disable database-level change tracking if it's enabled
        IF EXISTS (SELECT 1 FROM sys.change_tracking_databases WHERE database_id = DB_ID())
        BEGIN
            ALTER DATABASE CURRENT SET CHANGE_TRACKING = OFF;
        END
    END
    ELSE -- Enable
    BEGIN
        -- Enable database-level change tracking if not already enabled
        IF NOT EXISTS (SELECT 1 FROM sys.change_tracking_databases WHERE database_id = DB_ID())
        BEGIN
            ALTER DATABASE CURRENT SET CHANGE_TRACKING = ON;
        END

        WHILE @Index <= @TableCount
        BEGIN
            SELECT @TableName = TableName FROM @Tables WHERE ID = @Index;

            IF NOT EXISTS (
                SELECT 1 FROM sys.change_tracking_tables 
                WHERE object_id = OBJECT_ID(QUOTENAME(@Schema) + '.' + QUOTENAME(@TableName))
            )
            BEGIN
                SET @SQL = 'ALTER TABLE [' + @Schema + '].[' + @TableName + '] ENABLE CHANGE_TRACKING';
                EXEC (@SQL);
            END

            SET @Index += 1;
        END
    END
END
GO
